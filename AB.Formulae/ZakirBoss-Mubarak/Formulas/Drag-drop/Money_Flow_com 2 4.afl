_SECTION_BEGIN("Money_Flow_com");
// Money Flow Indicator - FVE by Markos Katsanos - almost identical to proprietary TSV money flow indicator by Worden

Period = Param("Period for FVE", 22, 5, 80, 1 );
Coeff = Param("Coeff for Cutoff", 0.1, 0, 2, 0.01 );

intra=log(H)-log(L);
Vintra = StDev(intra, period );
inter = log(Avg)-log(Ref(Avg,-1));
Vinter = StDev(inter,period);
Cutoff = Coeff * (Vinter+Vintra)*C;
MF = C- (H+L)/2 + Avg - Ref( Avg, -1 );
VC = IIf( MF > Cutoff, V,
IIf( MF < -Cutoff, -V, 0 ));
FVE = 100 * Sum( VC, Period )/(MA( V, Period ) * Period );

// Momemtum Indicator by William Blau

TSI = 100 * ( EMA( EMA( C - Ref( C, -1 ) ,25 ) ,13)
/ EMA( EMA( abs( C - Ref( C, -1) ),25 ), 13 ) );

// Plots for money flow and momentum indicators

Plot(TSI,"TSI",43,1); //Plots TSI line

PlotGrid(25,55); // shows white grid line for black background
PlotGrid(-25,-55); // same

Plot(EMA(TSI,7),"TSI signal",32,1); // Plots a 7 day EMA of TSI

Plot( FVE, "Modified FVE", colorRed, styleThick ); // FVE line is shown in red
Plot (0,"",colorBlue,styleNoLabel); // shows a blue line through 0


RAD_TO_DEG = 180/3.1415926; // radians to degrees
LinRegAngleFVE = RAD_TO_DEG * atan( LinRegSlope( FVE, 22 ) ); //calculates angle of money flow indicator


_SECTION_END();

Buy = Cross(TSI,EMA(TSI,7));
Sell = Cross(EMA(TSI,7),TSI);
PlotShapes(Buy*shapeUpArrow,colorGreen,0,Graph0);
PlotShapes(Sell*shapeDownArrow,colorRed,0,Graph0);
